//set to do not auto interrupt
if not listexists 'BodyTypes'
  createlist 'BodyTypes'
  //human
  pushlist 'BodyTypes' 0x190
  pushlist 'BodyTypes' 0x191
  //wraith
  pushlist 'BodyTypes' 0x2ec
  pushlist 'BodyTypes' 0x2eb
  //elf
  pushlist 'BodyTypes' 0x25d
  pushlist 'BodyTypes' 0x25e
  //lich
  //  pushlist 'BodyTypes' 0x2ed
  //vamp
  //  pushlist 'BodyTypes' 0x2e9
  // pushlist 'BodyTypes' 0x2e8
endif
if not listexists 'ghosts'
  createlist 'ghosts'
  pushlist 'ghosts' 0x192
  pushlist 'ghosts' 0x193
  pushlist 'ghosts' 0x260
  pushlist 'ghosts' 0x25f
endif
if not timerexists 'nospammsg'
  createtimer 'nospammsg'
endif
//set this to specify how often Criminal/Blue/Murderer/Paralyzed msgs go off
if timer 'nospammsg' > 1500
  @settimer 'nospammsg' 0
  @unsetalias 'sent'
endif
if not timerexists 'nospammortal'
  createtimer 'nospammortal'
endif
//set this to specify how often Mortal messages go off
if timer 'nospammortal' > 500
  @settimer 'nospammortal' 0
  @unsetalias 'sentmortal'
endif
//res dead friends if res precast
clearignorelist
if targetexists 'beneficial'
  for 0 to 'ghosts'
    while @findtype ghosts[] 'any' ground 1 1
      if @infriendlist 'found' or @inparty 'found'
        if not @property '[SL]' 'found' and not @property '[Min]' 'found'
          if not @property '[TB]' 'found' and not @property '[CoM]' 'found'
            target! 'found'
          else
            if not @findalias 'sent'
              headmsg 'Faction' 43 'found'
              @setalias 'sent' 'self'
            endif
          endif
        else
          if not @findalias 'sent'
            headmsg 'Faction' 43 'found'
            @setalias 'sent' 'self'
          endif
        endif
      endif
      ignoreobject 'found'
    endwhile
  endfor
endif
//paralyzed friends
clearignorelist
ignoreobject 'self'
for 0 to 'BodyTypes'
  while @findtype BodyTypes[] 'any' ground 0 10
    if not hits 'found' == 0
      if paralyzed 'found'
        if @inparty 'found' or @infriendlist 'found'
          cast 'clumsy' 'found'
        else
          if not @findalias 'sent'
            headmsg 'Paralyzed' 95 'found'
            @setalias 'sent' 'self'
          endif
        endif
      endif
    endif
    ignoreobject 'found'
  endwhile
endfor
//find lowest hp friend
clearignorelist
ignoreobject 'self'
@setalias 'buddy' 'self'
for 0 to 'BodyTypes'
  while @findtype BodyTypes[] 'any' ground 0 10
    if not hits 'found' == 0
      if not @findalias 'buddy'
        if @inparty 'found' or @infriendlist 'found'
          if murderer 'found' and @inregion 'guards' 'found' 10
            if not @findalias 'sent'
              headmsg 'Murderer in guards!' 33 'found'
              @setalias 'sent' 'self'
            endif
          elseif criminal 'found' and @inregion 'guards' 'found' 10
            if not @findalias 'sent'
              headmsg 'Criminal in guards!' 1000 'found'
              @setalias 'sent' 'self'
            endif
          elseif @property '[SL]' 'found' or @property '[Min]' 'found'
            if not @findalias 'sent'
              headmsg 'Faction' 43 'found'
              @setalias 'sent' 'self'
            endif
          elseif @property '[TB]' 'found' or @property '[CoM]' 'found'
            if not @findalias 'sent'
              headmsg 'Faction' 43 'found'
              @setalias 'sent' 'self'
            endif
          else
            @setalias 'buddy' 'found'
          endif
        endif
      endif
      if not yellowhits 'found'
        if @infriendlist 'found' or @inparty 'found'
          if hits 'found' < hits 'buddy'
            if murderer 'found' and @inregion 'guards' 'found' 10
              if not @findalias 'sent'
                headmsg 'Murderer in guards!' 33 'found'
                @setalias 'sent' 'self'
              endif
            elseif criminal 'found' and @inregion 'guards' 'found' 10
              if not @findalias 'sent'
                headmsg 'Criminal in guards!' 1000 'found'
                @setalias 'sent' 'self'
              endif
            elseif @property '[SL]' 'found' or @property '[Min]' 'found'
              if not @findalias 'sent'
                headmsg 'Faction' 43 'found'
                @setalias 'sent' 'self'
              endif
            elseif @property '[TB]' 'found' or @property '[CoM]' 'found'
              if not @findalias 'sent'
                headmsg 'Faction' 43 'found'
                @setalias 'sent' 'self'
              endif
            else
              @setalias 'buddy' 'found'
            endif
          endif
        endif
      else
        if not @findalias 'sentmortal'
          headmsg 'Mortaled!' 54 'found'
          @setalias 'sentmortal' 'self'
        endif
      endif
    endif
    ignoreobject 'found'
  endwhile
endfor
if targetexists 'server' or targetexists 'beneficial'
  target! 'buddy'
endif
if poisoned 'buddy' and @inrange 'buddy' 10
  cast 'cure' 'buddy'
elseif hits 'buddy' < 95 and @inrange 'buddy' 10
  cast 'heal' 'buddy'
endif
//@playmacro 'ParaMortal'
